SourceFile =
    Item*

Item =
    TypeDeclaration

// Types
TypeDeclaration =
    'type' Name GenericParameterList? ConstraintList? '=' body:AnonymousType

AnonymousType =
      StructType
    | EnumType
    | TraitType

Type =
      NamedType
    | StructType
    | EnumType
    | TraitType

NamedType =
    'box'? Name GenericArgumentList?

NamedTypeList =
    types:(NamedType (',' NamedType)* ','?)?

StructType =
    'struct' body:StructFieldList

StructFieldList =
    '{' 
        fields:(StructField (',' StructField)* ','?)?
    '}'

StructField =
    Name ':' Type

EnumType =
    'enum' EnumVariantList

EnumVariantList =
    '{'
        variants:(EnumVariant (',' EnumVariant)* ','?)?
    '}'

EnumVariant =
    Name body:EnumVariantBody?

EnumVariantBody =
      EnumVariantBodyTypeList
    | EnumVariantBodyExpr

EnumVariantBodyTypeList = 
    '(' TypeList ')'

EnumVariantBodyExpr =
    '=' value:Expr

TraitType =
    'trait' '{' '}'

TypeList =
    types:(Type (',' Type)* ','?)?

// Generics
GenericParameterList =
    '[' params:(Name (',' Name)* ','?) ']'

GenericArgumentList =
    '[' types:(NamedType (',' NamedType)* ','?)? ']'

ConstraintList =
    Constraint*

Constraint =
    'where' param:Name ':' constraints:TypeList

// Expressions
Expr =
      BinaryExpr
    | InterpolatedString
    | Literal
    | Name

BinaryExpr =
    lhs:Expr
    op:(
        '||' | '&&'
        | '==' | '!=' | '<=' | '>=' | '<' | '>'
        | '+' | '*' | '-' | '/' | '%' | '<<' | '>>' | '^' | '|' | '&'
        | '=' | '+=' | '/=' | '*=' | '%=' | '>>=' | '<<=' | '-=' | '|=' | '&=' | '^='
    )
    rhs:Expr

InterpolatedString =
    start:'string' parts:InterpolatedStringParts* end:'string'

InterpolatedStringParts =
    value:('string' | InterpolatedStringSlot)

InterpolatedStringSlot =
    '$' '{' Expr '}'

Name =
    'identifier'

Literal =
    value:(
        'int' | 'float'
        | 'string' | 'char' | 'byte'
        | 'true' | 'false'
    )
