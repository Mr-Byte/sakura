SourceFile =
    Item*

Item =
    TypeDefinition

TypeDefinition =
    'type' Name '=' body:Type
    
Type = 
      Name
    | StructType
    | EnumType
    | TraitType

TypeList =
    types:(Type (',' Type)* ','?)?

StructType =
    'struct' body:StructFieldDefinitionList

StructFieldDefinitionList =
    '{' 
        fields:(StructField (',' StructField)* ','?)?
    '}'

StructField =
    Name ':' Type

EnumType =
    'enum' EnumVariantList

EnumVariantList =
    '{'
        variants:(EnumVariant (',' EnumVariant)* ','?)?
    '}'

EnumVariant =
    Name body:EnumVariantBody?

EnumVariantBody =
      EnumVariantBodyTypeList
    | EnumVariantBodyExpr

EnumVariantBodyTypeList = 
    '(' TypeList ')'

EnumVariantBodyExpr =
    '=' value:Expr

TraitType =
    'trait' '{' '}'

Expr =
      BinaryExpr
    | InterpolatedString
    | Literal

BinaryExpr =
    lhs:Expr
    op:(
        '||' | '&&'
        | '==' | '!=' | '<=' | '>=' | '<' | '>'
        | '+' | '*' | '-' | '/' | '%' | '<<' | '>>' | '^' | '|' | '&'
        | '=' | '+=' | '/=' | '*=' | '%=' | '>>=' | '<<=' | '-=' | '|=' | '&=' | '^='
    )
    rhs:Expr

InterpolatedString =
    start:StringLiteral parts:InterpolatedStringParts* end:StringLiteral

InterpolatedStringParts =
    (StringLiteral | InterpolatedStringSlot)

InterpolatedStringSlot =
    '$' '{' Expr '}'

Name =
    'identifier'

// TODO: Add better code gen support for manually implemented enums.
StringLiteral =
    value:'string'

Literal =
    value:(
        'int' | 'float'
        | 'string' | 'char' | 'byte'
        | 'true' | 'false'
    )
