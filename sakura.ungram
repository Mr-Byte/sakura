SourceFile =
    Item*

Item =
    TypeDefinition

TypeDefinition =
    'type' Name '=' body:Type
    
Type = 
      Name
    | StructType
    | EnumType
    | TraitType

// TODO: Restrict type list to named types and struct types.
TypeList =
    types:(Type (',' Type)* ','?)?

StructType =
    'struct' StructFieldDefinitionList

StructFieldDefinitionList =
    '{' 
        fields:(StructField (',' StructField)* ','?)?
    '}'

StructField =
    Name ':' Type

EnumType =
    'enum' EnumVariantList

EnumVariantList =
    '{'
        variants:(EnumVariant (',' EnumVariant)* ','?)?
    '}'

EnumVariant =
    Name EnumVariantBody?

EnumVariantBody =
      EnumVariantBodyTypeList
    | EnumVariantBodyLiteral

EnumVariantBodyTypeList = 
    '(' TypeList ')'

EnumVariantBodyLiteral =
    '=' Literal

TraitType =
    'trait' '{' '}'

Name =
    'identifier'

Literal =
    value:('int_literal'
        | 'float_literal'
        | 'string_literal'
        | 'char_literal'
        | 'byte_literal'
        | 'true_literal'
        | 'false_literal')
